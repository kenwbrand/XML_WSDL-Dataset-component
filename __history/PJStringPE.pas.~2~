(*
  CHANGES:
    1) disabled minimize button of dialog form
*)

{ ##
  @PROJECT_NAME             Extended String Property Editor.
  @PROJECT_DESC             Property editor for properties of type string and
                            TCaption that extends default editor to provide a
                            dialog for editing multi-line strings.
  @FILE                     PJStringPE.pas
  @COMMENTS                 Property editor source code.
  @AUTHOR                   Peter Johnson, LLANARTH, Ceredigion, Wales, UK
  @OWNER                    DelphiDabbler
  @EMAIL                    peter.johnson@openlink.org
  @WEBSITE                  http://www.delphidabbler.com/
  @COPYRIGHT                © Peter D Johnson, 2004.
  @LEGAL_NOTICE             This property editor is distributed under the
                            Mozilla Public License - see below.
  @CREDITS                  Richard C Haven for the Ctrl+Return and Esc key
                            functionality.
  @HISTORY(
    @REVISION(
      @VERSION              1.0
      @DATE                 30/06/2004
      @COMMENTS             Original version.
    )
    @REVISION(
      @VERSION              1.1
      @DATE                 12/08/2004
      @COMMENTS             + Disabled minimize button and system menu item.
                            + Made form respond to Ees key to cancel dialog and
                              Ctrl+Return to OK the dialog.
    )
  )
}


{
 * ***** BEGIN LICENSE BLOCK *****
 *
 * Version: MPL 1.1
 *
 * The contents of this file are subject to the Mozilla Public License Version
 * 1.1 (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at http://www.mozilla.org/MPL/
 *
 * Software distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License for
 * the specific language governing rights and limitations under the License.
 *
 * The Original Code is Extended String Property Editor.
 *
 * The Initial Developer of the Original Code is Peter Johnson
 * (http://www.delphidabbler.com/).
 *
 * Portions created by the Initial Developer are Copyright (C) 2004 Peter
 * Johnson. All Rights Reserved.
 *
 * Contributor(s):
 *   Richard C Haven
 *
 * ***** END LICENSE BLOCK *****
}


unit PJStringPE;


interface


// Determine compiler
{$IFDEF VER140}
  {$DEFINE DELPHI6ANDUP}
{$ENDIF}
{$IFDEF VER150}
  {$DEFINE DELPHI6ANDUP}
  {$DEFINE DELPHI7ANDUP}
{$ENDIF}


uses
  // Delphi
  StdCtrls, Controls, ExtCtrls, Classes, Dialogs, Forms,
  DesignIntf, DesignEditors;


type

  {*
  TPJStringPEDlg:
    Dialog box used to edit multi-line string properties.
  }
  TPJStringPEDlg = class(TForm)
    edText: TMemo;
    pnlButton: TPanel;
    btnOK: TButton;
    btnCancel: TButton;
    btnLoad: TButton;
    btnSave: TButton;
    dlgOpen: TOpenDialog;
    dlgSave: TSaveDialog;
    bvlFrame: TBevel;
    cbWordWrap: TCheckBox;
    procedure btnLoadClick(Sender: TObject);
    procedure btnSaveClick(Sender: TObject);
    procedure cbWordWrapClick(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure FormDestroy(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
  private
    {*
      SaveSetting(): Saves given setting as binary data in registry.
      @param ID name of the value's registry value.
      @param Value untyped value to be written to registry.
      @param Size size of Value in bytes.
      @return True on success or false on error.
    }
    function SaveSetting(const ID: string; var Value;
      const Size: Integer): Boolean;
    {*
      ReadSetting(): Reads data for a given setting.
      @param ID name of the registry value storing setting data.
      @param Value untyped value to recieve setting data.
      @param Size size of Value in bytes.
      @return True if setting read successfully, false on error.
    }
    function ReadSetting(const ID: string; var Value;
      const Size: Integer): Boolean;
    {*
      UpdateWordWrap(): Updates editor's word wrap settings and check box.
      @param Flag whether we require word wrapping.
    }
    procedure UpdateWordWrap(Flag: Boolean);
  end;


  {*
  TPJStringPE:
    Property editor for hot text component's Code property.
  }
  TPJStringPE = class(TStringProperty)
  public
    {*
      GetAttributes(): Tell object inspector that editor displays a dialog box
      in addition to other attributes of a standard string property editor.
      @return a set of values describing attributes of property editor.
    }
    function GetAttributes: TPropertyAttributes; override;
    {*
      Edit(): Display property editor dialog box to edit the property.
    }
    procedure Edit; override;
  end;


{*
  Register(): Registers property editor for all string and TCaption properties
  of all components.
}
procedure Register;


implementation


uses
  // Delphi
  SysUtils, Windows, Registry;


{$R *.DFM}    // links the property editor form


{*
  Register(): Registers property editor for all string and TCaption properties
  of all components.
}
procedure Register;
begin
  RegisterPropertyEditor(
    TypeInfo(string),             // use for any string component
    nil,                          // use for any component
    '',                           // use for any property
    TPJStringPE);                 // property editor class
  RegisterPropertyEditor(
    TypeInfo(TCaption),           // use for any TCaption component
    nil,                          // use for any component
    '',                           // use for any property
    TPJStringPE);                 // property editor class
end;


{ TPJStringPE }

{*
  Edit(): Display property editor dialog box to edit the property.
}
procedure TPJStringPE.Edit;
begin
  with TPJStringPEDlg.Create(Application) do
    try
      edText.Text := GetStrValue;
      if ShowModal = mrOK then
        SetStrValue(edText.Text);
    finally
      Free;
    end;
end;

{*
  GetAttributes(): Tell object inspector that editor displays a dialog box
  in addition to other attributes of a standard string property editor.
  @return a set of values describing attributes of property editor.
}
function TPJStringPE.GetAttributes: TPropertyAttributes;
begin
  Result := inherited GetAttributes + [paDialog];
end;


{ TPJStringPEDlg }

const
  // Registry key where property editors settings are stored
  cRegKey = '\Software\delphiDabbler\Experts\StringPE';

{*
  btnLoadClick(): Event handler for "Load" button. Gets file from user and loads
  its contents in editor.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.btnLoadClick(Sender: TObject);
begin
  if dlgOpen.Execute then
    edText.Lines.LoadFromFile(dlgOpen.FileName);
end;

{*
  btnSaveClick(): Event handler for "Save" button. Saves contents of editor to
  file specified by user.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.btnSaveClick(Sender: TObject);
begin
  if dlgSave.Execute then
    edText.Lines.SaveToFile(dlgSave.FileName);
end;

{*
  cbWordWrapClick(): Event handler for "Word Wrap" check box. Updates word
  wrapping in editor.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.cbWordWrapClick(Sender: TObject);
begin
  UpdateWordWrap(cbWordWrap.Checked);
end;

{*
  FormDestroy(): Form destruction event handler. Saves persistent settings.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.FormDestroy(Sender: TObject);
var
  WordWrap: Boolean;    // Whether editor word wraps
  Pl: TWindowPlacement; // Placement of editor window
begin
  // Save word wrap value
  WordWrap := cbWordWrap.Checked;
  SaveSetting('WordWrap', WordWrap, SizeOf(WordWrap));
  // Save window placement
  FillChar(Pl, 0, SizeOf(Pl));
  Pl.Length := SizeOf(TWindowPlacement);
  GetWindowPlacement(Self.Handle, @Pl);
  SaveSetting(
    'WindowPlacement', Pl.rcNormalPosition, SizeOf(Pl.rcNormalPosition)
  );
end;

{*
  FormKeyDown(): Form key down event handler. Handles ESC and CTRL+RETURN key
  presses to simulate clicking of Cancel and OK buttons respectively.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
  case Key of
    VK_ESCAPE:  // ESC key - cancel dialog
      ModalResult := mrCancel;
    VK_RETURN:  // CTRL+RETURN - OK the dialog
    begin
      if Shift = [ssCtrl] then
        ModalResult := mrOK;
    end;
  end;
end;

{*
  FormShow(): Form show event handler. Reads persistent settings and applies
  them.
  @param Sender object generating event.
}
procedure TPJStringPEDlg.FormShow(Sender: TObject);
var
  WordWrap: Boolean;    // whether editor word wraps
  Pl: TWindowPlacement; // placement of editor window
  WorkArea: TRect;      // desktop work area
begin
  // Get word wrapping setting and update accordingly
  if not ReadSetting('WordWrap', WordWrap, SizeOf(WordWrap)) then
    WordWrap := False;
  UpdateWordWrap(WordWrap);
  // Get window placement setting and place window accordingly
  FillChar(Pl, SizeOf(Pl), #0);
  if ReadSetting(
    'WindowPlacement', Pl.rcNormalPosition, SizeOf(Pl.rcNormalPosition)
  ) then
  begin
    // we have read settings: position and size window accordingly
    Pl.Length := SizeOf(TWindowPlacement);
    Pl.showCmd := SW_SHOW;      // needed when restore called late in start-up
    SetWindowPlacement(Self.Handle, @Pl);
  end
  else
  begin
    // we have no settings: centre window on Windows workarea
    if SystemParametersInfo(SPI_GETWORKAREA, 0, @WorkArea, 0) then
    begin
      Left := WorkArea.Left + (WorkArea.Right - WorkArea.Left - Width) div 2;
      Top := WorkArea.Top + (WorkArea.Bottom - WorkArea.Top - Height) div 2;
      if Left < WorkArea.Left then
        Left := WorkArea.Left;
      if Top < WorkArea.Top then
        Top := WorkArea.Top;
    end;
  end;
end;

{*
  ReadSetting(): Reads data for a given setting.
  @param ID name of the registry value storing setting data.
  @param Value untyped value to recieve setting data.
  @param Size size of Value in bytes.
  @return True if setting read successfully, false on error.
}
function TPJStringPEDlg.ReadSetting(const ID: string; var Value;
  const Size: Integer): Boolean;
begin
  with TRegistry.Create do
    try
      try
        Result := OpenKeyReadOnly(cRegKey) and ValueExists(ID);
        if Result then
          Result := ReadBinaryData(ID, Value, Size) = Size;
      except
        Result := False;
      end;
    finally
      Free;
    end;
end;

{*
  SaveSetting(): Saves given setting as binary data in registry.
  @param ID name of the value's registry value.
  @param Value untyped value to be written to registry.
  @param Size size of Value in bytes.
  @return True on success or false on error.
}
function TPJStringPEDlg.SaveSetting(const ID: string; var Value;
  const Size: Integer): Boolean;
begin
  with TRegistry.Create do
    try
      try
        Result := OpenKey(cRegKey, True);
        if Result then
          WriteBinaryData(ID, Value, Size);
      except
        Result := False;
      end;
    finally
      Free;
    end;
end;

{*
  UpdateWordWrap(): Updates editor's word wrap settings and check box.
  @param Flag whether we require word wrapping.
}
procedure TPJStringPEDlg.UpdateWordWrap(Flag: Boolean);
const
  // map of flag to TMemo.Scrollbars property value
  cScrollBars: array[Boolean] of TScrollStyle = (ssBoth, ssVertical);
begin
  // update check box
  cbWordWrap.Checked := Flag;
  // update editor memo control
  edText.WordWrap := Flag;
  edText.ScrollBars := cScrollBars[Flag];
end;

end.
